// --------------------------------------------------------------------------
//
// ARGENT: Heavy Assault Rifle
//
// --------------------------------------------------------------------------

ACTOR ArgHeavyAR : ArgWeapon replaces Chaingun
{
	Weapon.SelectionOrder 6
	Weapon.AmmoUse 1
	Weapon.AmmoGive 20
	Weapon.AmmoType "Clip"
	Inventory.PickupMessage "$GOTHEAVY"
	Tag "$TAG_HEAVY"
	DamageType "ArgHeavyAR"

	States
	{
	Spawn:
		HARP A -1
		Stop
	Ready:
		HARG A 1 A_WeaponReady
		Loop
	Select:
		HARG A 0
		Goto SelectLoop
	Deselect:
		HARG A 0
		Goto DeselectLoop
	Fire:
		HARG B 1 Bright {
			A_GunFlash;
			A_FireBullets(2.0, 1.0, -1, 7, "ArgBulletPuff");
			A_PlaySound("weapons/heavy/fire", CHAN_AUTO);
		}
		HARG C 1 Bright A_WeaponOffset(1.0 + frandom(-0.25, 0.25), 33.0 + frandom(-0.25, 0.25))
		HARG D 1 Bright A_WeaponOffset(3.0 + frandom(-0.25, 0.25), 34.0 + frandom(-0.25, 0.25))
		HARG A 1        A_WeaponOffset(2.0 + frandom(-0.25, 0.25), 33.0 + frandom(-0.25, 0.25))
		HARG A 1        A_WeaponOffset(1.0 + frandom(-0.25, 0.25), 32.0 + frandom(-0.25, 0.25))
		HARG A 0 A_Refire
		Goto Ready
	Flash:
		TNT1 A 1 Bright A_Light2
		TNT1 A 1 Bright A_Light1
		Goto LightDone
	}
}
