// --------------------------------------------------------------------------
//
// ARGENT: Pistol
//
// --------------------------------------------------------------------------

ACTOR ArgPistol : ArgWeapon replaces Pistol
{
	Weapon.SelectionOrder 8
	Inventory.PickupMessage "$ARG_GOT_PISTOL"
	Tag "$ARG_TAG_PISTOL"
	Obituary "$ARG_OB_PISTOL"
	DamageType "ArgPistol"
	
	+WEAPON.NOAUTOFIRE
	+WEAPON.WIMPY_WEAPON
	
	States
	{
	Spawn:
		PISP A -1
		Stop
	Ready:
		PISG A 1 A_WeaponReady
		Loop
	Select:
		PISG A 0
		Goto SelectLoop
	Deselect:
		PISG A 0
		Goto DeselectLoop
	Fire:
		PISF A 1 Bright {
			A_GunFlash;
			A_FireBullets(1.5, 1, 1, 5, "ArgPistolPuff");
			A_PlaySound("weapons/pistol/fire", CHAN_AUTO);
			A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH);
		}
		PISF B 1   A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH) 
		PISF C 1 { A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH); A_WeaponOffset(1.0 + frandom(-0.25, 0.25), 33.0 + frandom(-0.25, 0.25)); }
		PISF C 1 { A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH); A_WeaponOffset(2.0 + frandom( 0.00, 1.00), 37.0 + frandom( 0.00, 1.00)); }
		PISF B 1 { A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH); A_WeaponOffset(3.0 + frandom( 0.00, 1.00), 40.0 + frandom( 0.00, 2.00)); }
		PISF B 1 { A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH); A_WeaponOffset(1.0 + frandom(-0.25, 0.25), 38.0 + frandom(-0.25, 0.25)); }
		PISG A 1   A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
		PISG AAAAA 1 A_WeaponReady
		PISG A 0 A_Refire
		Goto Ready
	Flash:
		TNT1 A 2 Bright A_Light1
		Goto LightDone
	}
}

ACTOR ArgPistolPuff : ArgBulletPuff
{
	Translation "80:111=[143,143,255]:[0,0,83]", "160:167=[143,143,255]:[0,0,83]", "208:223=[143,143,255]:[0,0,83]", "224:231=[143,143,255]:[199,199,255]"
	States
	{
	Spawn:
		XPUF Q 0 BRIGHT
		XPUF Q 0 A_SpawnItemEx("ArgPuffSmoke",0,0,4.0+0.1*random(-10,10),0,0,0,0,SXF_CLIENTSIDE,0)
		XPUF Q 0 A_Jump(128,"PuffMirrored")
	PuffNormal:
		XPUF QRSTU 1 BRIGHT
		XPUF FGH 1 BRIGHT
		Stop
	PuffMirrored:
		XPUF VWXYZ 1 BRIGHT
		XPUF NOP 1 BRIGHT
		Stop
	}
}
