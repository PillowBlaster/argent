// --------------------------------------------------------------------------
//
// ARGENT: Base Weapon Definitions
//
// --------------------------------------------------------------------------

ACTOR ArgWeapon : DoomWeapon
{
	Weapon.UpSound   "weapons/swap"
	Weapon.BobStyle  "InverseSmooth"
	Weapon.BobSpeed  2.00
	Weapon.BobRangeX 0.75
	Weapon.BobRangeY 0.50
	Weapon.SelectionOrder 9999999
	Scale 0.75
	States
	{
	Spawn:
		TNT1 A 1
		Stop
	Ready:
		TNT1 A 1 A_WeaponReady
		Loop
	Select:
	SelectLoop:
		"####" "#" 0 A_Raise
		"####" "#" 1 A_Raise
		Loop
	Deselect:
	DeselectLoop:
		"####" "#" 0 A_Lower
		"####" "#" 1 A_Lower
		Loop
	Fire:
		TNT1 A 1
		Goto Ready
	}
}

ACTOR ArgModWeapon : ArgWeapon
{
	States
	{
	
	/* Ready Mod-Jump */
	
	Ready:
		"####" "#" 0 A_JumpIf(CallACS("ModSelected", 1), "Mod1Ready")
		"####" "#" 0 A_JumpIf(CallACS("ModSelected", 2), "Mod2Ready")
		"####" "#" 0 A_Jump(256, "Mod0Ready")
	Mod0Ready:
	Mod1Ready:
	Mod2Ready:
	ReadyLoop:
		"####" "#" 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
		
	/* Select Mod-Jump */
	
	Select:
		"####" "#" 0 A_JumpIf(CallACS("ModSelected", 1), "Mod1Select")
		"####" "#" 0 A_JumpIf(CallACS("ModSelected", 2), "Mod2Select")
		"####" "#" 0 A_Jump(256, "Mod0Select")
	Mod0Select:
	Mod1Select:
	Mod2Select:
		Goto SelectLoop
		
	/* Fire Mod-Jump */
	
	Fire:
		"####" "#" 0 A_JumpIf(CallACS("ModSelected", 1), "Mod1Fire")
		"####" "#" 0 A_JumpIf(CallACS("ModSelected", 2), "Mod2Fire")
		"####" "#" 0 A_Jump(256, "Mod0Fire")
	Mod0Fire:
	Mod1Fire:
	Mod2Fire:
		"####" "#" 1
		Goto Ready
		
	/* Deselect doesn't need a mod jump since it's just "####" anyway. */
	
	Deselect:
		Goto DeselectLoop
		
	/* Mod Swapping. Uses the Reload bind for convenience. */
	
	Reload:
		"####" "#" 0 ACS_NamedExecute("SwapMod")
		"####" "#" 0 A_JumpIf(CallACS("ModSelected", 1), "Mod1Swap")
		"####" "#" 0 A_JumpIf(CallACS("ModSelected", 2), "Mod2Swap")
	Mod1Swap:
	Mod2Swap:
		"####" "#" 1 A_WeaponReady
		Goto Ready
	}
}

ACTOR ArgModToken : ArgToken
{
	Inventory.MaxAmount 2
}
