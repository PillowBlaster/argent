// --------------------------------------------------------------------------
//
// ARGENT: Ammo
//
// --------------------------------------------------------------------------

ACTOR ArgClip       : Clip       replaces Clip       { Inventory.PickupSound "items/ammo" Scale 0.66 }
ACTOR ArgClipBox    : ClipBox    replaces ClipBox    { Inventory.PickupSound "items/ammo" Scale 0.66 }
ACTOR ArgShell      : Shell      replaces Shell      { Inventory.PickupSound "items/ammo" Scale 0.66 }
ACTOR ArgShellBox   : ShellBox   replaces ShellBox   { Inventory.PickupSound "items/ammo" Scale 0.66 }
ACTOR ArgCell       : Cell       replaces Cell       { Inventory.PickupSound "items/ammo" Scale 0.75 }
ACTOR ArgCellPack   : CellPack   replaces CellPack   { Inventory.PickupSound "items/ammo" Scale 0.75 }

ACTOR ArgRocketAmmo : Ammo replaces RocketAmmo
{
	Inventory.PickupSound "items/ammo"
	Inventory.PickupMessage "$GOTROCKET"
	Inventory.Amount 1
	Inventory.MaxAmount 30
	Ammo.BackpackAmount 1
	Ammo.BackpackMaxAmount 60
	Inventory.Icon "ROCKA0"
	Scale 0.66
	States
	{
	Spawn:
		ROCK A -1
		Stop
	}
}

ACTOR ArgRocketBox : ArgRocketAmmo replaces RocketBox
{
	Inventory.PickupMessage "$GOTROCKBOX"
	Inventory.Amount 5
	Scale 0.5
	States
	{
	Spawn:
		BROK A -1
		Stop
	}
}

ACTOR ArgBFGAmmo : Ammo
{
	Inventory.PickupSound "items/ammo"
	Inventory.PickupMessage "$ARG_GOT_BFGCELL"
	Inventory.Icon BFGCA0
	Inventory.Amount 1
	Inventory.MaxAmount 5
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 10
	Scale 0.66
	+FLOATBOB
	States
	{
	Spawn:
		BFGC BC 5 Bright 
		Loop
	}
}

ACTOR ArgBFGAmmoPickup : CustomInventory
{
	Inventory.PickupSound "items/ammo"
	Inventory.PickupMessage "$ARG_GOT_BFGCELL"
	Scale 0.66
	+FLOATBOB
	States
	{
	Spawn:
		BFGC BC 5 Bright 
		Loop
	Pickup:
		TNT1 A 0 {
			A_GiveInventory("ArgBFGAmmo", 1);
			A_RemoveSiblings(true, RMVF_MISC|RMVF_EVERYTHING);
		}
		Stop
	}
}

ACTOR ArgBFGAmmoStand
{
	Scale 0.66
	+FORCEYBILLBOARD //:V
	States
	{
	Spawn:
		BFGC A -1
		Stop
	}
}

ACTOR ArgBFGAmmoSpawner
{
	+NOINTERACTION
	States
	{
	Spawn:
		TNT1 A -1 NoDelay {
			A_SpawnItemEx("ArgBFGAmmoStand" ,0,0,0,0,0,0,0,SXF_SETMASTER|SXF_CLIENTSIDE     );
			A_SpawnItemEx("ArgBFGAmmoPickup",0,0,0,0,0,0,0,SXF_SETMASTER|SXF_TRANSFERSPECIAL); //Juuuust to be sure, in case of mappers doing things.
		}
		Stop
	}
}
